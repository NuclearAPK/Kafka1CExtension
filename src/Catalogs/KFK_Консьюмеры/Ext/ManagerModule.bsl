#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция АктивныеКонсьюмеры() Экспорт   
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	KFK_Консьюмеры.Активен КАК Активен,
	               |	KFK_Консьюмеры.Идентификатор КАК Идентификатор,
	               |	KFK_Брокеры.АдресБрокера КАК АдресБрокера,
	               |	KFK_Консьюмеры.РасписаниеЗадания КАК РасписаниеЗадания,
	               |	KFK_Консьюмеры.КаталогЛогов КАК КаталогЛогов,
	               |	KFK_Консьюмеры.Ссылка КАК Ссылка,
	               |	KFK_Консьюмеры.ТаймаутОжиданияСообщений КАК ТаймаутОжиданияСообщений,
	               |	KFK_Консьюмеры.ПолучатьСообщенияДвоичнымиДанными КАК ПолучатьСообщенияДвоичнымиДанными,
	               |	KFK_Консьюмеры.Брокер КАК Брокер
	               |ПОМЕСТИТЬ ВТ_АктивныеКонсьюмеры
	               |ИЗ
	               |	Справочник.KFK_Консьюмеры КАК KFK_Консьюмеры
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.KFK_Брокеры КАК KFK_Брокеры
	               |		ПО KFK_Консьюмеры.Брокер = KFK_Брокеры.Ссылка
	               |ГДЕ
	               |	KFK_Консьюмеры.Активен = ИСТИНА
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	KFK_КонсьюмерыТопики.Топик КАК Топик,
	               |	ВТ_АктивныеКонсьюмеры.Ссылка КАК Ссылка
	               |ИЗ
	               |	ВТ_АктивныеКонсьюмеры КАК ВТ_АктивныеКонсьюмеры
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.KFK_Консьюмеры.Топики КАК KFK_КонсьюмерыТопики
	               |		ПО ВТ_АктивныеКонсьюмеры.Ссылка = KFK_КонсьюмерыТопики.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	KFK_БрокерыПараметрыПодключения.Ключ КАК Ключ,
	               |	KFK_БрокерыПараметрыПодключения.Значение КАК Значение,
	               |	ВТ_АктивныеКонсьюмеры.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.KFK_Брокеры.ПараметрыПодключения КАК KFK_БрокерыПараметрыПодключения
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АктивныеКонсьюмеры КАК ВТ_АктивныеКонсьюмеры
	               |		ПО KFK_БрокерыПараметрыПодключения.Ссылка = ВТ_АктивныеКонсьюмеры.Брокер
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	KFK_КонсьюмерыПараметрыКонсьюмера.Ключ КАК Ключ,
	               |	KFK_КонсьюмерыПараметрыКонсьюмера.Значение КАК Значение,
	               |	KFK_КонсьюмерыПараметрыКонсьюмера.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.KFK_Консьюмеры.ПараметрыКонсьюмера КАК KFK_КонсьюмерыПараметрыКонсьюмера
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АктивныеКонсьюмеры КАК ВТ_АктивныеКонсьюмеры
	               |		ПО KFK_КонсьюмерыПараметрыКонсьюмера.Ссылка = ВТ_АктивныеКонсьюмеры.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_АктивныеКонсьюмеры.Активен КАК Активен,
	               |	ВТ_АктивныеКонсьюмеры.АдресБрокера КАК АдресБрокера,
	               |	ВТ_АктивныеКонсьюмеры.Идентификатор КАК ИдентификаторГруппы,
	               |	ВТ_АктивныеКонсьюмеры.РасписаниеЗадания КАК РасписаниеЗадания,
	               |	ВТ_АктивныеКонсьюмеры.КаталогЛогов КАК КаталогЛогов,
	               |	ВТ_АктивныеКонсьюмеры.Ссылка КАК Ссылка,
	               |	ВТ_АктивныеКонсьюмеры.ТаймаутОжиданияСообщений КАК ТаймаутОжидания,
	               |	ВТ_АктивныеКонсьюмеры.ПолучатьСообщенияДвоичнымиДанными КАК ПолучатьСообщенияДвоичнымиДанными
	               |ИЗ
	               |	ВТ_АктивныеКонсьюмеры КАК ВТ_АктивныеКонсьюмеры";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаОсновныеДанные = РезультатЗапроса[РезультатЗапроса.Количество()-1].Выбрать();
	ВыборкаПараметры = РезультатЗапроса[РезультатЗапроса.Количество()-2].Выбрать();     
	ВыборкаПараметрыБрокера = РезультатЗапроса[РезультатЗапроса.Количество()-3].Выбрать();
    ВыборкаТопики = РезультатЗапроса[РезультатЗапроса.Количество()-4].Выбрать();   
	
	Пока ВыборкаОсновныеДанные.Следующий() Цикл                 
		
		СтруктураКоньюмера = ПолучитьСтруктуруКонсьюмера();
		ЗаполнитьЗначенияСвойств(СтруктураКоньюмера, ВыборкаОсновныеДанные);
		
		ВыборкаТопики.Сбросить();                               
		Пока ВыборкаТопики.НайтиСледующий(Новый Структура("Ссылка", ВыборкаОсновныеДанные.Ссылка)) Цикл
			СтруктураКоньюмера.Топики.Добавить(ВыборкаТопики.Топик);
		КонецЦикла;                                             
		
		// добавим параметры брокера   
		ВыборкаПараметрыБрокера.Сбросить();
		Пока ВыборкаПараметрыБрокера.НайтиСледующий(Новый Структура("Ссылка", ВыборкаОсновныеДанные.Ссылка)) Цикл
 			СтруктураПараметров = Новый Структура("Ключ, Значение");
			ЗаполнитьЗначенияСвойств(СтруктураПараметров, ВыборкаПараметрыБрокера);
			СтруктураКоньюмера.Параметры.Добавить(СтруктураПараметров);			
		КонецЦикла;
		
		// параметры консьюмера
		ВыборкаПараметры.Сбросить();
		Пока ВыборкаПараметры.НайтиСледующий(Новый Структура("Ссылка", ВыборкаОсновныеДанные.Ссылка)) Цикл
			СтруктураПараметров = Новый Структура("Ключ, Значение");
			ЗаполнитьЗначенияСвойств(СтруктураПараметров, ВыборкаПараметры);
			СтруктураКоньюмера.Параметры.Добавить(СтруктураПараметров);
		КонецЦикла;  
		
		Результат.Добавить(СтруктураКоньюмера);
		
	КонецЦикла;            
	
	Возврат Результат;
		
КонецФункции      

#КонецОбласти    

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруКонсьюмера()   
	
	СтруктураКоньюмера = Новый Структура("ИдентификаторГруппы, АдресБрокера, Активен, РасписаниеЗадания, ТаймаутОжидания, КаталогЛогов, Ссылка, 
										|ПолучатьСообщенияДвоичнымиДанными, Топики, Параметры",,,,,,,,, Новый Массив, Новый Массив);
										
	Возврат СтруктураКоньюмера;
	
КонецФункции

#КонецОбласти

#КонецЕсли